
//This short magma script verifies the calculations of Remark 2.3.3, using the implementation of quaternion algebras in magma.

Q:=Rationals();

Ci:=Matrix([[1,0,0,0],[0,1,0,0],[0,0,-1,0],[0,0,0,-1]]);
Cj:=Matrix([[1,0,0,0],[0,-1,0,0],[0,0,1,0],[0,0,0,-1]]);
Comega:=Matrix([[1,0,0,0],[0,1,0,0],[0,0,-1/2,1/2],[0,0,-3/2,-1/2]]);

//This function checks the D1 case of the remark, and returns the F2-dimension of (O/2O)^G, where O is a maximal order in the quaternion algebra (a,b)_{Q}, and G is the order 2 group generated by conjugation by i. The three examples below realize all the possibilities.

fixedpointsD1mod2 := function(a,b)                
A:=QuaternionAlgebra<Q|a,b>;
O:=MaximalOrder(A);
B:=BasisMatrix(O);
Ci:=Matrix([[1,0,0,0],[0,1,0,0],[0,0,-1,0],[0,0,0,-1]]);
T:=B*Ci*B^(-1);
T2:=Matrix(GF(2),T); 
return Dimension(Kernel(T2-IdentityMatrix(GF(2),4)));         
end function;


print("D1 examples: dimension of (O/2O)^G");
fixedpointsD1mod2(10,3);
fixedpointsD1mod2(-1,3);
fixedpointsD1mod2(5,7);


//This function checks the D2 case, and returns the F2-dimension of (O/2O)^G, where O is a maximal order in the quaternion algebra (a,b)_{Q}, and G is the order 4 group generated by the involutions given by conjugating by i and j. The two examples below realize all the possibilities.


fixedpointsD2mod2:=function(a,b)
A<i,j,k>:=QuaternionAlgebra<Q|a,b>;
O:=MaximalOrder(A);
B:=BasisMatrix(O);
Ti:=Matrix(GF(2),B*Ci*B^(-1));
Tj:=Matrix(GF(2),B*Cj*B^(-1));
I4:=IdentityMatrix(GF(2),4);
return Dimension(Kernel(Ti-I4) meet Kernel(Tj-I4));
end function;


print("D2 examples: dimension of (O/2O)^G");
fixedpointsD2mod2(7,5);
fixedpointsD2mod2(-1,3);


//This function checks the D3 case, and returns the F3-dimension of (O/3O)^G, where O is a maximal order in the quaternion algebra (a,b)_{Q}, and G = <[1-\omega,j]> is the order 6 dihedral group described in Lemma 2.2.4 of the paper. The two examples below realize all the possibilities.


fixedpointsD3mod3:=function(a)
A<i,j,k>:=QuaternionAlgebra<Q|-3,a>;
O:=MaximalOrder(A);
B:=BasisMatrix(O);
Tomega:=Matrix(GF(3),B*Comega*B^(-1));
Tj:=Matrix(GF(3),B*Cj*B^(-1));
I4:=IdentityMatrix(GF(3),4);
return Dimension(Kernel(Tomega-I4) meet Kernel(Tj-I4));
end function;


print("D3 examples: dimension of (O/3O)^G");
fixedpointsD3mod3(2);
fixedpointsD3mod3(15);

